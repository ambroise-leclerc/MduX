cmake_minimum_required (VERSION 3.18)

option(BUILD_SHARED_LIBS "Enable compilation of shared libraries" OFF)
option(ENABLE_CEF "Enable Chromium Embedded Framework" ON)
option(ENABLE_TESTING "Enable Test Builds" ON)
option(ENABLE_FUZZING "Enable Fuzzing Builds" OFF)

set(VERSION_STRING_REGEX "inline constexpr std::string_view version = \"([0-9]+\\.[0-9]+\\.[0-9]+)\"")
file(STRINGS "${CMAKE_CURRENT_SOURCE_DIR}/include/WebFront.hpp" VERSION_STRING REGEX ${VERSION_STRING_REGEX})
string(REGEX MATCH "([0-9]+\\.[0-9]+\\.[0-9]+)" VERSION_STRING "${VERSION_STRING}")
project(WebFront LANGUAGES CXX VERSION ${VERSION_STRING})

if(ENABLE_TESTING)
  enable_testing()
endif()

add_library(WebFront INTERFACE)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/cmake)
include(StandardProjectSettings)
include(PreventInSourceBuilds)
add_library(WebFront_options INTERFACE)
target_compile_features(WebFront_options INTERFACE cxx_std_23)
if(APPLE)
  target_compile_definitions(WebFront_options INTERFACE __cpp_lib_format=202207L)
endif()

# Apply compiler-specific settings
configure_compiler_settings(WebFront_options)

add_library(WebFront_warnings INTERFACE)

include(Cache)

include(Linker)
configure_linker(WebFront_options)

include(CompilerWarnings)
set_project_warnings(WebFront_warnings)

include(Sanitizers)
enable_sanitizers(WebFront_options)

configure_file(Doxyfile.in ${CMAKE_BINARY_DIR}/Doxyfile)
include(Doxygen)
enable_doxygen()

include(StaticAnalyzers)
include(CPM)

CPMAddPackage("gh:ambroise-leclerc/networking-ts-impl@1.12.0a")
if (ENABLE_CEF)
  # Use static runtime library to match CEF wrapper
  if(MSVC)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
  endif()
  
  # Temporarily disable ccache for CEF builds to avoid compiler flag conflicts
  set(ORIGINAL_CXX_COMPILER_LAUNCHER ${CMAKE_CXX_COMPILER_LAUNCHER})
  unset(CMAKE_CXX_COMPILER_LAUNCHER)
  
  CPMAddPackage("gh:ambroise-leclerc/CEF@137.0.17d")
  
  # Restore ccache for other builds
  set(CMAKE_CXX_COMPILER_LAUNCHER ${ORIGINAL_CXX_COMPILER_LAUNCHER})
endif()

target_link_libraries(WebFront INTERFACE WebFront_options WebFront_warnings)

# Apply static runtime library to all targets when CEF is enabled
if(ENABLE_CEF AND MSVC)
  set_property(TARGET WebFront_options PROPERTY MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
  set_property(TARGET WebFront_warnings PROPERTY MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
endif()

target_include_directories(WebFront INTERFACE ${CMAKE_SOURCE_DIR}/include)
include_directories(SYSTEM ${networking-ts-impl_SOURCE_DIR}/include)


add_compile_definitions(_WIN32_WINNT=0x0501)

if(ENABLE_TESTING)
  add_subdirectory(test)
  add_subdirectory(webtest)
endif()

add_subdirectory(src)
